*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        List_Network
Test Template     List_Network Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    List
Valid Test to List network with default value
                      [Tags]    main_scenarios
                      OK        All networks

Valid Test to List only active network
                      OK        Only active networks

Valid Test to List only inactive network
                      OK        Only inactive networks

*** Keywords ***
Startup
    Log    *******Start Test "LIST NETWORK"*******
    Log    *******Connect to AE******
    Connect AE

List_Network Template
    [Arguments]    ${OK_NOK}    ${List}
    [Documentation]    This test list Networks as per specified parameter selected for listing
    ...    _Precondition :- To run this test suit Unix Agent is configured and running
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${List}: This field Specify which virtual networks you want to list.
    Generate String
    Init Action    ${_LIST_NETWORK_ACTION}
    Action Set    &UC4RB_KVM_LIST_TYPE#    ${List}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_KVM_NETWORK_COUNT#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_KVM_NETWORK_COUNT#
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_KVM_NETWORK_LIST#    ELSE    Action Object Variable Should Not Be Found    &UC4RB_KVM_NETWORK_LIST#

Teardown
    Log    *******End Test "LIST NETWORK"*******
