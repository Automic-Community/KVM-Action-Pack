*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        Start_KVM_VMS
Default Tags
Test Template     Start KVM VMS Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    VM_Name_or_UUID     Fail_if_running
Start VM with valid VM name and default values
                      [Tags]    main_scenarios
                      OK        ${VM_To_Start}      NO

Start VM with valid UUID
                      [Tags]    main_scenarios
                      OK        ${UUID_To_Start}    NO

Invalid test for invalid VM Name to Start VM
                      NOK       Invalid             NO

Mandatory test for UUID/VM name required to start VM
                      NOK       ${Empty}            NO

Start VM when VM already started and Fail if running is YES
                      NOK       ${VM_To_Start}      YES

Start VM when VM already started and Fail if running is NO
                      OK        ${VM_To_Start}      NO

*** Keywords ***
Startup
    Log    *******Start Test "START KVM VM"*******
    Log    *******Connect to AE*******
    Connect AE

Start KVM VMS Template
    [Arguments]    ${OK_NOK}    ${VM_Name_or_UUID}    ${Fail_if_running}
    [Documentation]    This test start a VM as per specified parameter value
    ...    _Precondition :- To run this test suit Unix Agent is configured and running and atleast on VM should be there to stop
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${VM_Name_or_UUID}: Provide name or UUID of VM you want to stop.
    ...    -${Fail_if_running}: Specify whether if you want to fail action if VM is already started. Default value is 'No'..
    Generate String
    Init Action    ${_START_KVM_VM_ACTION}
    Run Keyword If    '${VM_Name_or_UUID}' == 'Invalid'    Action Set    &UC4RB_KVM_VM_NAME_UUID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_KVM_VM_NAME_UUID#    ${VM_Name_or_UUID}
    Action Set    &UC4RB_KVM_FAIL_IF_RUNNING#    ${Fail_if_running}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure

Teardown
    Log    *******End Test "START KVM VM"*******
