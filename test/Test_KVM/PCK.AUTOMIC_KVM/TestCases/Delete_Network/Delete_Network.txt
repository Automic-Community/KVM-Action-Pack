*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        Delete_Network
Default Tags
Test Template     Delete KVM Network Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    Network_or_UUID                      Delete_if_active
Delete Network with valid Persistent Network name which is Active and delete if active NO
                      [Tags]    main_scenarios
                      OK        ${Active_Network_To_Delete}          NO

Delete Network with valid Persistent Network name which is Active and delete if active YES
                      [Tags]    main_scenarios
                      OK        ${Active_Network_To_Delete}          YES

Delete Network with valid UUID of Persistent network which is Inactive with default value Delete if active.
                      [Tags]    main_scenarios
                      OK        ${Inactive_NetworkUUID_To_Delete}    NO

Invalid test for invalid Network Name to Delete Network
                      NOK       Invalid                              NO

Mandatory test for UUID/Network name required to Delete a network
                      NOK       ${Empty}                             NO

*** Keywords ***
Startup
    Log    *******Start Test "DELETE KVM NETWORK"*******
    Log    *******Connect to AE*******
    Connect AE

Delete KVM Network Template
    [Arguments]    ${OK_NOK}    ${Network_or_UUID}    ${Delete_if_active}
    [Documentation]    This test deletes a Network.
    ...    _Precondition :- To run this test suit Unix Agent is configured and running and atleast one network should be there to delete
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${Network_or_UUID}: Provide name or UUID of Network you want to delete.
    ...    -${Delete_if_active} : Specify if you want to remove action network or not.
    Generate String
    Init Action    ${_DELETE_NETWORK_ACTION}
    Run Keyword If    '${Network_or_UUID}' == 'Invalid'    Action Set    &UC4RB_KVM_NW_NAME_UUID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_KVM_NW_NAME_UUID#    ${Network_or_UUID}
    Action Set    &UC4RB_KVM_REMOVE_IF_ACTIVE#    ${Delete_if_active}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure

Teardown
    Log    *******End Test "DELETE KVM NETWORK"*******
