*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        Resume_KVM_VMS
Default Tags
Test Template     Resume KVM VMS Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    VM_Name_or_UUID
Resume VM with valid VM name which is paused
                      [Tags]    main_scenarios
                      OK        ${VM_To_Resume}

Resume VM with valid VM UUID which is paused
                      [Tags]    main_scenarios
                      OK        ${UUID_To_Resume}

Invalid test for invalid VM Name to Resume VM
                      NOK       Invalid

Mandatory test for UUID/VM name required to Resume VM
                      NOK       ${Empty}

Resume VM when VM already resumed/running
                      OK        ${VM_To_Resume}

Resume VM when VM in shutoff state
                      NOK       ${VM_Shutoff}

*** Keywords ***
Startup
    Log    *******Start Test "RESUME KVM VM"*******
    Log    *******Connect to AE*******
    Connect AE

Resume KVM VMS Template
    [Arguments]    ${OK_NOK}    ${VM_Name_or_UUID}
    [Documentation]    This test resume a VM .
    ...    _Precondition :- To run this test suit Unix Agent is configured and running and atleast on VM should be there to resume
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${VM_Name_or_UUID}: Provide name or UUID of VM you want to resume.
    Generate String
    Init Action    ${_RESUME_KVM_VM_ACTION}
    Run Keyword If    '${VM_Name_or_UUID}' == 'Invalid'    Action Set    &UC4RB_KVM_VM_NAME_UUID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_KVM_VM_NAME_UUID#    ${VM_Name_or_UUID}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure

Teardown
    Log    *******End Test "RESUME KVM VM"*******
