*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        Shutdown_KVM_VMS
Default Tags
Test Template     Shutdown KVM VMS Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    VM_Name_or_UUID          Shutdown_Mode
Shutdown VM with valid VM name and default shutdown mode
                      [Tags]    main_scenarios
                      OK        ${VM_To_Shutdown}        ${Empty}

Shutdown VM with valid UUID and default shutdown mode
                      [Tags]    main_scenarios
                      OK        ${UUID_To_Shutdown}      ${Empty}

Invalid test for invalid VM Name to Shutdown VM
                      NOK       Invalid                  ${Empty}

Mandatory test for UUID/VM name required to shutdown VM
                      NOK       ${Empty}                 ${Empty}

Shutdown VM when VM already shut off
                      NOK       ${VM_Already_Shutoff}    ${Empty}

Shutdown VM with acpi shutdown mode
                      OK        ${VM_To_Shutdown}        acpi

*** Keywords ***
Startup
    Log    *******Start Test "SHUTDOWN KVM VM"*******
    Log    *******Connect to AE*******
    Connect AE

Shutdown KVM VMS Template
    [Arguments]    ${OK_NOK}    ${VM_Name_or_UUID}    ${Shutdown_Mode}
    [Documentation]    This test shutdown a VM as per specified parameter value
    ...    _Precondition :- To run this test suit Unix Agent is configured and running and atleast on VM should be present and running for shutdown
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${VM_Name_or_UUID}: Provide name or UUID of VM you want to shutdown.
    ...    -${Shutdown_Mode}: Specify shutdown mode.
    Generate String
    Init Action    ${_SHUTDOWN_KVM_VM_ACTION}
    Run Keyword If    '${VM_Name_or_UUID}' == 'Invalid'    Action Set    &UC4RB_KVM_VM_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_KVM_VM_NAME#    ${VM_Name_or_UUID}
    Action Set    &UC4RB_KVM_MODE#    ${Shutdown_Mode}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure

Teardown
    Log    *******End Test "SHUTDOWN KVM VM"*******
