*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        Save_KVM_VMS_To_File
Default Tags
Test Template     Save KVM VM To File Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    VMName            TargetFile       Bypass_Cache    Restore_State
Save VM to file with valid VM name
                      [Tags]    main_scenarios
                      OK        ${VMName}         ${TargetFile}    NO              Empty

Save VM to file with restore state as running
                      OK        ${VMName1}        ${TargetFile}    NO              Running

Mandatory Check for VM name/UUID field
                      NOK       ${Empty}          ${TargetFile}    NO              Running

Mandatory Check for Target file field
                      NOK       ${VMName}         ${Empty}         NO              Paused

Check with Invalid VM name / UUID
                      NOK       Invalid           ${TargetFile}    NO              Paused

*** Keywords ***
Startup
    Log    *******Start Test "SAVE KVM VM TO FILE"*******
    Log    *******Connect to AE*******
    Connect AE

Save KVM VM to File Template
    [Arguments]    ${OK_NOK}    ${VMName}    ${TargetFile}    ${Bypass_Cache}    ${Restore_State}
    [Documentation]    This test restore a VM as per specified parameter value
    ...    _Precondition :- To run this test suit Unix Agent is configured and running and atleast on VM should be there to delete
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${VMName}: Provide KVM VM name to be saved.
    ...    -${TargetFile}: Specify VM state to be set after restoring.
    ...    -${Bypass_Cache}: Specify whether if you want to avoid file system cache while restoring.
    ...    -${Restore_State}: Specify VM state to be set after restoring..
    Generate String
    Init Action    ${_SAVE_KVM_VM_FROM_FILE_ACTION}
    Run Keyword If    '${VMName}' == 'Invalid'    Action Set    &UC4RB_KVM_VM_NAME#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_KVM_VM_NAME#    ${VMName}
    Action Set    &UC4RB_KVM_TRG_FILE#    ${TargetFile}
    Action Set    &UC4RB_KVM_SKIP_CACHE#    ${Bypass_Cache}
    Action Set    &UC4RB_KVM_RESTORE_STATE#    ${Restore_State}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure

Teardown
    Log    *******End Test "SAVE KVM VM From File"*******
