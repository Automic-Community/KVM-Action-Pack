*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        Suspend_KVM_VMS
Default Tags
Test Template     Suspend KVM VMS Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    VM_Name_or_UUID
Suspend VM with valid VM name which is in running state.
                      [Tags]    main_scenarios
                      OK        ${VM_To_Suspend}

Suspend VM with valid UUID of VM which is in running state.
                      [Tags]    main_scenarios
                      OK        ${UUID_To_Suspend}

Invalid test for invalid VM Name to Suspend VM
                      NOK       Invalid

Mandatory test for UUID/VM name required to suspend VM
                      NOK       ${Empty}

Suspend VM when VM already paused/suspended
                      OK        ${VM_To_Suspend}

Suspend VM when VM is shutoff
                      NOK       ${Shutoff_VM}

*** Keywords ***
Startup
    Log    *******Start Test "SUSPEND KVM VM"*******
    Log    *******Connect to AE*******
    Connect AE

Suspend KVM VMS Template
    [Arguments]    ${OK_NOK}    ${VM_Name_or_UUID}
    [Documentation]    This test suspend/pause a VM.
    ...    _Precondition :- To run this test suit Unix Agent is configured and running and atleast on VM should be there to suspend VM.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${VM_Name_or_UUID}: Provide name or UUID of VM you want to suspend.
    Generate String
    Init Action    ${_SUSPEND_KVM_VM_ACTION}
    Run Keyword If    '${VM_Name_or_UUID}' == 'Invalid'    Action Set    &UC4RB_KVM_VM_NAME_UUID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_KVM_VM_NAME_UUID#    ${VM_Name_or_UUID}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure

Teardown
    Log    *******End Test "SUSPEND KVM VM"*******
