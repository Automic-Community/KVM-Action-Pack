*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        Stop_Network
Default Tags
Test Template     Stop KVM Network Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    Network_or_UUID
Stop Network with valid Persistent Network name which is Active
                      [Tags]    main_scenarios
                      OK        ${Network_To_Stop}

Stop Network with valid UUID of Persistent network which is Active
                      [Tags]    main_scenarios
                      OK        ${NetworkUUID_To_Stop}

Invalid test for invalid Network Name to Stop Network
                      NOK       Invalid

Mandatory test for UUID/Network name required to stop a network
                      NOK       ${Empty}

Stop Network when Network already stopped/Inactive
                      OK        ${Network_To_Stop}

Stop Network when Network is Transient Network and Active.
                      OK        ${Transient_Network}

*** Keywords ***
Startup
    Log    *******Start Test "STOP KVM NETWORK"*******
    Log    *******Connect to AE*******
    Connect AE

Stop KVM Network Template
    [Arguments]    ${OK_NOK}    ${Network_or_UUID}
    [Documentation]    This test stop a Network.
    ...    _Precondition :- To run this test suit Unix Agent is configured and running and atleast one network should be there to stop
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${Network_or_UUID}: Provide name or UUID of Network you want to stop...
    Generate String
    Init Action    ${_STOP_NETWORK_ACTION}
    Run Keyword If    '${Network_or_UUID}' == 'Invalid'    Action Set    &UC4RB_KVM_NW_NAME_UUID#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_KVM_NW_NAME_UUID#    ${Network_or_UUID}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure

Teardown
    Log    *******End Test "STOP KVM NETWORK"*******
