*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        Delete_KVM_VMS
Default Tags
Test Template     Delete KVM VMS Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    VM_Name/UUID      Remove_State_File    Remove_all_Storage    Remove_specific_Storage    Wipe_Storage    Remove_Snapshot_Metadata    Remove_nvram_File
Delete available VMS with VM_Name
                      [Tags]    main_scenarios
                      OK        new-vm-clone1     NO                   NO                    ${EMPTY}                   NO              NO                          NO

Delete available VMS with UUID
                      OK        ${UUID}           NO                   NO                    ${EMPTY}                   NO              NO                          NO

Delete KVM VM by removing all storage
                      OK        new-vm-clone3     NO                   YES                   ${EMPTY}                   NO              NO                          NO

*** Keywords ***
Startup
    Log    *******Start Test "DELETE KVM VMs"*******
    Log    *******Connect to AE*******
    Connect AE

Delete KVM VMS Template
    [Arguments]    ${OK_NOK}    ${VM_Name/UUID}    ${Remove_State_File}    ${Remove_all_Storage}    ${Remove_specific_Storage}    ${Wipe_Storage}
    ...    ${Remove_Snapshot_Metadata}    ${Remove_nvram_File}
    [Documentation]    This test delete a VM as per specified parameter value
    ...    _Precondition :- To run this test suit Unix Agent is configured and running and atleast on VM should be there to delete
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${VM_Name/UUID}: Specify name or UUID of VM you want to remove
    ...    -${Remove_State_File}: Specify whether if you want to remove domain managed state file.
    ...    -${Remove_all_Storage}: Specify whether if you want to remove all associated storage volumes.
    ...    -${Remove_specific_Storage}: Provide name of storage volumes as separated by comma. If this field is provided 'Remove all Storage' field is ignored.
    ...    -${Wipe_Storage}: Specify whether if you want to wipe data on removed volumes..
    ...    -${Remove_Snapshot_Metadata}: Specify whether if you want remove all domain snapshot metadata, if inactive
    ...    -${Remove_nvram_File}: Specify whether if you want remove remove nvram file, if inactive.
    Generate String
    Init Action    ${_DELETE_KVM_VM_ACTION}
    Action Set    &UC4RB_KVM_VM_NAME_UUID#    ${VM_Name/UUID}
    Action Set    &UC4RB_KVM_MANAGED_SAVE#    ${Remove_State_File}
    Action Set    &UC4RB_KVM_REMOVE_ALL_STORAGE#    ${Remove_all_Storage}
    Action Set    &UC4RB_KVM_STORAGE_VOLUMES#    ${Remove_specific_Storage}
    Action Set    &UC4RB_KVM_WIPE_STORAGE#    ${Wipe_Storage}
    Action Set    &UC4RB_KVM_SNAPSHOT_METADATA#    ${Remove_Snapshot_Metadata}
    Action Set    &UC4RB_KVM_NVRAM#    ${Remove_nvram_File}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure

Teardown
    Log    *******End Test "DELETE KVM VMs"*******
