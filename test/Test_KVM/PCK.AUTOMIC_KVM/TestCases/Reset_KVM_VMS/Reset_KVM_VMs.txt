*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        Reset_KVM_VMS
Default Tags
Test Template     Reset KVM VMS Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    VM_Name/UUID
Reset available VM with valid VM_Name
                      [Tags]    main_scenarios
                      OK        new-vm-clone1

Reset available VM with valid UUID
                      OK        fb0b9f26-53b5-46fa-8619-0d388fc3e7aa

Reset VM by Empty VM name
                      NOK       ${EMPTY}

Reset VM by Invalid VM name
                      NOK       Invalid_vm

Resest VM with Invalid UUID
                      NOK       ${UUID}

*** Keywords ***
Startup
    Log    *******Start Test "RESET KVM VMs"*******
    Log    *******Connect to AE*******
    Connect AE

Reset KVM VMS Template
    [Arguments]    ${OK_NOK}    ${VM_Name/UUID}
    [Documentation]    This test reset a VM as per specified parameter value
    ...    _Precondition :- To run this test suit Unix Agent is configured and running and atleast on VM should be there to reset
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${VM_Name/UUID}: Specify name or UUID of VM you want to reset
    Generate String
    Init Action    ${_RESET_KVM_VM_ACTION}
    Action Set    &UC4RB_KVM_VM_NAME_UUID#    ${VM_Name/UUID}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure

Teardown
    Log    *******End Test "RESET KVM VMs"*******
