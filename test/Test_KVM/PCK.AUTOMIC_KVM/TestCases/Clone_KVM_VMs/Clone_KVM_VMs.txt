*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        Clone_KVM_VMS
Test Template     Clone_KVM_VMS Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    Original_Guest       Original_XML    Auto_Clone    New_Name    New_Diskfile    Preserve_Data    New_MAC_Address    Replace
Clone_KVM_VM with Original_Guest Name
                      [Tags]    main_scenarios
                      OK        ${Original_Guest}    ${EMPTY}        YES           ${EMPTY}    ${EMPTY}        NO               ${EMPTY}           NO

Clone_KVM_VM with no Original Guest and Original XML
                      [Tags]    main_scenarios
                      NOK       ${EMPTY}             ${EMPTY}        YES           ${EMPTY}    ${EMPTY}        NO               ${EMPTY}           NO

*** Keywords ***
Startup
    Log    *******Start Test "CLONE KVM VMs"*******
    Log    *******Connect to AE******
    Connect AE

Clone_KVM_VMS Template
    [Arguments]    ${OK_NOK}    ${Original_Guest}    ${Original_XML}    ${Auto_Clone}    ${New_Name}    ${New_Diskfile}
    ...    ${Preserve_Data}    ${New_MAC_Address}    ${Replace}
    [Documentation]    This test Clone a VM as per specified parameter value
    ...    _Precondition :- To run this test suit Unix Agent is configured and running
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${Original_Guest}: This field provide name of the original guest; The status must be shut off or paused.
    ...    -${Original_XML}: This field provide XML file to use as the original guest.
    ...    -${Auto_Clone}: This field provide Auto generate clone name and storage paths from the original guest configuration.
    ...    -${New_Name}: This field Provide name for new VM. If provided Auto Clone input is ignored.
    ...    -${New_Diskfile}: This field Provide new file to use as the disk image for the new guest. If provided Auto Clone input is ignored.
    ...    -${Preserve_Data}: This field specifies to not clone storage, new disk images specified via New Diskfile are preserved unchanged.
    ...    -${New_MAC_Address}: This field provide New fixed MAC address for the clone guest. If not provided then system assigns one of available MAC Addresses.
    ...    -${Replace}: this field specifies not to check name collision, overwrite any guest with the same name.
    Generate String
    Init Action    ${_CLONE_KVM_VM_ACTION}
    Action Set    &UC4RB_KVM_ORG_GUEST#    ${Original_Guest}
    Action Set    &UC4RB_KVM_ORG_XML#    ${Original_XML}
    Action Set    &UC4RB_KVM_AUTO_CLONE#    ${Auto_Clone}
    Run Keyword If    '${New_Name}' == 'Invalid'    Action Set    &UC4RB_KVM_NEW_NAME##    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_KVM_NEW_NAME#    ${New_Name}
    Run Keyword If    '${New_Diskfile}' == 'Invalid'    Action Set    &UC4RB_KVM_NEW_DSKFILE#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_KVM_NEW_DSKFILE#    ${New_Diskfile}
    Action Set    &UC4RB_KVM_PRES_DATA#    ${Preserve_Data}
    Run Keyword If    '${New_MAC_Address}' == 'Invalid'    Action Set    &UC4RB_KVM_NEW_MAC_ADD#    ${Generated_Name}_${Time}    ELSE    Action Set
    ...    &UC4RB_KVM_NEW_MAC_ADD#    ${New_MAC_Address}
    Action Set    &UC4RB_KVM_REPLACE#    ${Replace}
    Set Overwrite Agent
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Assert Success    ELSE IF    '${OK_NOK}' == 'NOK'    Assert Failure

Teardown
    Log    *******End Test "CLONE KVM VMS"*******
